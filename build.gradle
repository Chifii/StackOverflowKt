buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$androidMavenGradlePlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinPluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"

        classpath("io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion") {
            //TODO: once Kotlin version matches with Detekt transitives one, we can delete this
            exclude group: 'org.jetbrains.kotlin'
        }
        classpath("io.gitlab.arturbosch.detekt:detekt-formatting:${detektVersion}") {
            exclude group: 'org.jetbrains.kotlin'
        }
        classpath "com.google.gms:google-services:$googleServices"
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    group = libraryGroupId
    version = libraryVersion
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        toolVersion = "$detektVersion"
        input = files("$projectDir")
        config = files("${rootProject.projectDir}/custom.detekt.yml")
        buildUponDefaultConfig = true
        autoCorrect = true
        parallel = true
        reports {
            xml {
                enabled = true
            }
            html {
                enabled = true
            }
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion maxApiLevel.toInteger()
                buildToolsVersion androidBuildToolsVersion

                defaultConfig {
                    applicationId "chifinaldo.stackoverflowkt"
                    minSdkVersion minApiLevel
                    targetSdkVersion maxApiLevel

                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                buildTypes {
                    debug {
                        minifyEnabled false
                    }
                    release {
                        minifyEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_11
                    targetCompatibility JavaVersion.VERSION_11
                }
                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_11.toString()
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}